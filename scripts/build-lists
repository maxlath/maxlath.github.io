#!/usr/bin/env coffee

buildList = require '../lib/build_list'
{ updateFile } = require '../lib/update_part'
buildIndex = require '../lib/build_index'
buildHead = require '../lib/build_head'
wrapList = require '../lib/wrap_list'
_ = require '../lib/utils'
{ sections, sectionsList } = require 'config'
marked = require 'marked'
buildTagsList = require '../lib/build_tags_list'

[ fast ] = process.argv.slice(2)
if fast then _.warn 'fast mode: not compiling tags'

itemsListsContent = sectionsList
  .map (section)->
    # build list
    html = buildList section
    data = _.getFolderData section
    # build sub indexes / SIDE EFFECT /
    buildIndex section, html
    # wrap lists for main index
    return wrapList html, 2, data, "/#{section}"
  .join '\n'

unless fast then tagsListHtml = buildTagsList()

# HOME
index = _.readFileSync './content.md'
tabs = _.readFileSync './partials/tabs.html'
links = _.readFileSync './partials/links.md'
about = _.readFileSync './partials/about.md'
data = _.getFolderData '.'
head = buildHead data
# add wrap lists to main index
index = updateFile index, head, 'head'
index = updateFile index, tabs, 'tabs'
index = updateFile index, itemsListsContent, 'itemslists'
unless fast then index = updateFile index, tagsListHtml, 'tags'
index = updateFile index, links, 'links'
index = updateFile index, about, 'about', true

if index.length > 0
  console.log 'writing home index.html'
  _.writeFile './index.html', index
else
  console.log 'updated index is empty'.red
